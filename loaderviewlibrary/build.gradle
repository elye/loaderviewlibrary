apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.elyeproj.libraries'
version = '1.2.2'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 9
        versionName "1.2.2"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.2.1'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

bintray {
    user = 'YOUR_USERNAME'
    key = 'YOUR_API_KEY'
    pkg {
        repo = 'maven'
        name = 'com.elyeproj.loaderviewlibrary'

        version {
            name = '1.2.2-LoaderView'
            desc = 'Loader View Widget'
            released  = new Date()
            vcsTag = '1.2.2'
        }

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/elye/loaderviewlibrary.git'
        websiteUrl = 'https://github.com/elye/loaderviewlibrary'
    }
    configurations = ['archives']
}

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}
